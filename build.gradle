buildscript {
  repositories {
  	mavenCentral()
  	maven {
  		name = "GradleRIO"
  		url = "http://dev.imjac.in/maven"
  	}
  }
  dependencies {
    classpath group: 'jaci.openrio.gradle', name: 'GradleRIO', version: '4.4.3'	    //+ for latest version		//Change this line if you wish to Update GradleRIO
  }
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'GradleRIO'                                 //Apply the GradleRIO plugin

gradlerio.robotClass = "com.team2502.robot2017.Robot"     //The class for the main Robot Class. Used in manifest
gradlerio.team = "2502"                                   //Your FRC team number (e.g. 5333 for team 'Can't C#', or 47 for Chief Delphi)
//gradlerio.rioIP = "10.53.33.20"                         //Uncomment to specify the IP address of the RIO

dependencies {
    compile "org.jetbrains:annotations:15.0" // Provides useful annotations like @NotNull & @Nullable
}

def robotManifest = {
    attributes 'Main-Class': 'edu.wpi.first.wpilibj.RobotBase'
    attributes 'Robot-Class': gradlerio.robotClass
}

task copyVersionFile(type: Exec) {
    workingDir '.git/hooks'
    commandLine './copyVersionFile.sh'
}

task copyHooks(type: Copy) {
    from 'hooks/'
    into '.git/hooks/'
}

jar {
    dependsOn copyHooks
    dependsOn copyVersionFile
    from configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    manifest robotManifest
}

task genJavadoc(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

artifacts {
  archives genJavadoc
}

genJavadoc.onlyIf { project.hasProperty('runJavadoc') }
javadoc.onlyIf { project.hasProperty('runJavadoc') }
